//~~~~ Settings for Gradle
buildscript {
    repositories {
        maven {
            url "https://plugins.gradle.org/m2/"
        }
        jcenter()
    }
    dependencies {
        classpath "org.kt3k.gradle.plugin:coveralls-gradle-plugin:2.8.1"
        classpath 'us.kirchmeier:gradle-capsule-plugin:1.0.2'
        classpath 'ch.netzwerg:gradle-release-plugin:1.2.4'
    }
}

apply plugin: 'java'
apply plugin: 'application'

group 'ro.ghionoiu'

//noinspection GroovyUnusedAssignment
targetCompatibility = JavaVersion.VERSION_1_8
//noinspection GroovyUnusedAssignment
sourceCompatibility = JavaVersion.VERSION_1_8

repositories {
    jcenter()
}

/**
 * Design:
 * Humble-video is composed of two jar files _noarch + native.
 * The _noarch contains the API while the native jar contains
 * the JNI libraries required to interface with the Native codecs
 *
 * So:
 * - the capsule is configured to only download the artifact required for that platform
 * - the test includes all the native libraries to be cross-platform
 * - there is no native library configured for the compile target
 */
ext {
    humbleVideoVersion = '0.2.1'
}

dependencies {
    compile 'io.humble:humble-video-noarch:'+humbleVideoVersion
    compile 'com.beust:jcommander:1.64'

    compile 'org.slf4j:slf4j-api:1.7.25'
    compile 'ch.qos.logback:logback-classic:1.1.3'
    compileOnly 'org.projectlombok:lombok:1.16.16'

    testCompile 'io.humble:humble-video-all:'+humbleVideoVersion
    testCompile 'com.google.zxing:core:3.3.0'
    testCompile('junit:junit:4.11'){
        exclude group: 'org.hamcrest', module: 'hamcrest-core'
    }
    testCompile 'org.hamcrest:hamcrest-core:1.3'
    testCompile 'org.hamcrest:hamcrest-library:1.3'
    testCompileOnly 'org.projectlombok:lombok:1.16.16'
}

// ~~~~~ Packaging

apply plugin: 'us.kirchmeier.capsule'

mainClassName = "tdl.record.screen.ScreenRecorderCliApp"
jar {
    manifest {
        attributes "Main-Class": "$mainClassName"
    }
}

task mavenCapsule(type: MavenCapsule){
    applicationClass mainClassName

    capsuleManifest {
        minJavaVersion = '1.8.0'
        dependencyConfiguration configurations.compile

        //noinspection GroovyAssignabilityCheck
        platform('windows'){
            dependencies << 'io.humble:humble-video-arch-x86_64-w64-mingw32:'+humbleVideoVersion
            dependencies << 'io.humble:humble-video-arch-i686-w64-mingw32:'+humbleVideoVersion
        }
        //noinspection GroovyAssignabilityCheck
        platform('macos'){
            dependencies << 'io.humble:humble-video-arch-x86_64-apple-darwin12:'+humbleVideoVersion
            dependencies << 'io.humble:humble-video-arch-i686-apple-darwin12:'+humbleVideoVersion
        }
        //noinspection GroovyAssignabilityCheck
        platform('linux'){
            dependencies << 'io.humble:humble-video-arch-x86_64-pc-linux-gnu6:'+humbleVideoVersion
            dependencies << 'io.humble:humble-video-arch-i686-pc-linux-gnu6:'+humbleVideoVersion
        }
    }
}

// ~~~~~ Gather coverage

apply plugin: "jacoco"
apply plugin: "com.github.kt3k.coveralls"

jacocoTestReport {
    reports {
        xml.enabled = true // coveralls plugin depends on xml format report
        html.enabled = true
    }
}

//~~~~~ Release management


/**
 * Prerequisites
 *    - Create package in Bintray
 *
 * Now you can use regular maven upload to push files to you package.
 * The version will be created automatically
 *
 * To push to Maven Central use Bintray sync features:
 *    - Created request for jcenter ( if missing )
 *    - Manually sync the two repos
 *
 * To publish new version use the Bintray UI:
 *    - publish current artifacts
 *    - sync with Maven Central
 */
apply plugin: "ch.netzwerg.release"
apply plugin: 'maven'

release {
    dependsOn build // 'distZip' could e.g be used in combination with the 'application' plugin
    push = false // 'true' would e.g. be useful when triggering the release task on a CI server
    versionSuffix = '-SNAPSHOT' // '.DEV' or '' (empty) could be useful alternatives
    tagPrefix = 'v' // 'r' or '' (empty) could be useful alternatives
}

private static String readSystem(String property) {
    System.getenv(property) != null  ? System.getenv(property) : "unset"
}

task sourceJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}
task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

// Add the sourceJars to non-extractor modules
artifacts {
    archives sourceJar
    archives javadocJar
}

uploadArchives {
    repositories.mavenDeployer {
        name = 'bintray-julianghionoiu-dev-screen-record'
        repository(id: 'bintray-julianghionoiu-dev-screen-record',
                url: 'https://api.bintray.com/maven/julianghionoiu/maven/dev-screen-record') {
            authentication(userName: readSystem("BINTRAY_USER"), password: readSystem("BINTRAY_KEY"))
        }

        pom.project {
            name 'Programming Session Recorder'
            description 'Library designed for recording programming sessions.'
            url 'https://github.com/julianghionoiu/dev-screen-record'

            developers {
                developer {
                    name 'Iulian (Julian) Ghionoiu'
                    email 'iulian.ghionoiu@gmail.com'
                    organization 'Iulian Ghionoiu'
                    organizationUrl 'https://github.com/julianghionoiu'
                }
            }

            scm {
                connection 'scm:git:git@github.com:julianghionoiu/dev-screen-record.git'
                developerConnection 'scm:git:git@github.com:julianghionoiu/dev-screen-record.git'
                url 'git@github.com:julianghionoiu/dev-screen-record.git'
            }

            licenses {
                license {
                    name 'Apache License, Version 2.0'
                    url 'https://www.apache.org/licenses/LICENSE-2.0.txt'
                }
            }
        }
    }
}